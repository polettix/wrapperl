#!/usr/bin/env perl
use strict;
use warnings;
use 5.006000;
use File::Spec::Functions qw<
  rel2abs
  splitpath
  splitdir
  catdir
  catpath
  file_name_is_absolute
>;
use Cwd qw< getcwd realpath >;
use Pod::Usage qw< pod2usage >;
my $VERSION = '0.02';

our $SUFFIX  = '.pl';
our $PERLDOC = 'perldoc';
our $PERL    = $^X;
our $ME      = absolute($0);

my @alternatives = (
   [qr{(?mxs: \A wrapperl \z)}, \&dispatch_wrapperl],
   [qr{(?mxs: \A perl \z)},     \&action_exec],
   [
      qr{(?mxs: \A perldoc \z)},
      sub {
         unshift @ARGV, $PERLDOC;
         action_sibling();
      },
   ],
   [
      qr{.},
      sub {
         unshift @ARGV, "$ME$SUFFIX";
         action_exec();
      },
   ],
);

my (undef, undef, $basename) = splitpath($ME);
for my $candidate (@alternatives) {
   my ($match, $callback) = @$candidate;
   next unless $basename =~ m{$match};
   $callback->();
}

die "something went really wrong\n";

sub absolute {
   my ($path) = @_;
   return $path if file_name_is_absolute($path);
   return rel2abs($path);
}

sub find_envfile {
   my @starts = @_;
   push @starts, getcwd(), $ENV{HOME} unless @starts;
   for my $start (@starts) {
      next unless -e $start;
      my ($volume, $directories) = splitpath($start, -d $start);
      my @path = splitdir($directories);
      my $envfile;
      while (@path) {
         my $candidate = catpath($volume, catdir(@path), 'wrapperl.env');
         return $candidate if -e $candidate;
         pop @path;
      }
   } ## end for my $start (@starts)
   die "could not find wrapperl.env\n";
   return;    # never reached
} ## end sub find_envfile

sub load_envfile {
   my $envfile = find_envfile(@_);
   my $retval  = do $envfile
     or die "errors loading '$envfile'\n";
   return $retval;
} ## end sub load_envfile

sub myexec {
   my @command = @_;
   exec {$command[0]} @command;
   die "failed execution of @command\n";
}

sub action_env {
   my $e = find_envfile(@ARGV ? $ARGV[0] : ());
   print {*STDOUT} "$e\n";
   exit 0;
}

sub action_sibling {
   my $sibling = shift @ARGV;
   pod2usage(
      -verbose  => 99,
      -sections => 'USAGE',
      message   => 'no sibling program provided'
   ) unless defined $sibling;

   load_envfile();
   my ($v, $d) = splitpath($PERL);
   my (undef, undef, $basename) = splitpath($sibling);
   my $path = catpath($v, $d, $basename);
   myexec($PERL, $path, @ARGV);
} ## end sub action_sibling

sub action_exec {
   load_envfile();
   myexec($PERL, @ARGV);
}

sub dispatch_wrapperl {
   my $first = $ARGV[0] || '';
   if ($first =~ m{\A(?: -e | --env )\z}mxs) {
      shift @ARGV;
      action_env();
   }
   elsif ($first =~ m{\A(?: -d | --doc )\z}mxs) {
      shift @ARGV;
      unshift @ARGV, $PERLDOC;
      action_sibling();
   }
   elsif ($first =~ m{\A(?: -s | --sibling )\z}mxs) {
      shift @ARGV;
      action_sibling();
   }

   # Last chance, we're to act as wrap-perl. Just make sure
   # get rid of the first parameter if invoked with -x | --exec
   shift @ARGV if $first =~ m{\A(?: -x | --exec )\z}mxs;
   action_exec();
} ## end sub dispatch_wrapperl

__END__

=pod

=encoding utf-8

=head1 NAME

wrapperl - wrapper for Perl customized invocation

=head1 TL;DR

... or an example is worth a whole manual sometimes.

First of all, download C<wrapperl> from
L<https://raw.githubusercontent.com/polettix/wrapperl/master/wrapperl>
and put somewhere in the environments where you need it. It is not
necessary to put it in a directory in the C<PATH>, although it is
suggested in order to access all functionalities.

Let's make a few assumptions:

=over

=item *

you will write your program C<prg.pl>. If
you don't even want to write one, you can copy and paste this:

   #!/usr/bin/env perl
   print "using perl '$^X', \@INC contains:\n";
   print "- '$_'\n" for @INC;

=item *

you do your coding in a development environment where:

=over

=item *

you develop C<prg.pl> inside directory C</home/me/program>

=item *

C<perl> is located at C</home/me/perl/bin/perl>

=item *

the libraries you need are stored in non-standard positions
C</path/to/some/lib> and C</path/to/another/lib>

=back

=item *

you deploy your program in a production environment with a different
setup, namely:

=over

=item *

your program C<prg.pl> is deployed in directory C</app/program>

=item *

C<perl> is located at C</approved/perl/bin/perl>

=item *

the libraries you need are all stored in C</approved/lib>

=back

=back

In both environments, you create a symbolic link named C<prg>
pointing towards C<wrapperl>. The link is located inside the same
directory as C<prg.pl>.

You create the C<wrapperl.env> file, which will hold configurations
that are specific for the specific environment it is located into.
In this example we will put it in the same directory as C<prg> and
C<prg.pl>.

This is what you end up with in the development environment:

   me@devhost /home/me/program$ ls -l
   lrwxrwxrwx 1 me me  8 Apr 23 22:51 prg -> /home/me/bin/wrapperl
   -rwxr-xr-x 1 me me 74 Apr 23 22:28 prg.pl
   -rwxr-xr-x 1 me me 90 Apr 22 12:35 wrapperl.env

   me@devhost /home/me/program$ cat wrapperl.env
   $ENV{PERL5LIB} = '/path/to/some/lib:/path/to/another/lib';
   $PERL = '/home/me/perl/bin/perl';

This is what you have in the production environment:

   me@production /app/program$ ls -l
   lrwxrwxrwx 1 me me  8 Apr 25 20:51 prg -> /usr/local/bin/wrapperl
   -rwxr-xr-x 1 me me 74 Apr 25 20:51 prg.pl
   -rwxr-xr-x 1 me me 66 Apr 25 20:51 wrapperl.env

   me@production /app/program$ cat wrapperl.env
   $ENV{PERL5LIB} = '/approved/lib';
   $PERL = '/approved/perl/bin/perl';

So yes, they two setups are mostly the same, except for the contents
of the C<wrapperl.env> files, each containing configurations that
are environment-specific.

Now, you are ready to run your program in either environment, just
remember to execute the symbolic link to C<wrapperl> instead of your
program.

In the development environment:

   me@devhost /home/me/program$ ./prg
   using perl '/home/me/perl/bin/perl', @INC contains:
   - '/path/to/another/lib/i686-linux'
   - '/path/to/another/lib'
   - '/path/to/some/lib/i686-linux'
   - '/path/to/some/lib'
   - '/home/me/perl/lib/site_perl/5.18.1/i686-linux'
   - '/home/me/perl/lib/site_perl/5.18.1'
   - '/home/me/perl/lib/5.18.1/i686-linux'
   - '/home/me/perl/lib/5.18.1'
   - '.'

In the production environment:

   me@production /app/program$ ./prg
   using perl '/approved/perl/bin/perl', @INC contains:
   - '/approved/lib/i686-linux'
   - '/approved/lib'
   - '/approved/perl/lib/site_perl/5.18.1/i686-linux'
   - '/approved/perl/lib/site_perl/5.18.1'
   - '/approved/perl/lib/5.18.1/i686-linux'
   - '/approved/perl/lib/5.18.1'
   - '.'

That's all folks!

=head1 SYNOPSYS

   # Minimal setup: create a "wrapperl.env" file.
   # It is a Perl program to set up the right environment and
   # define the $PERL variable that points to the desired perl
   shell$ cat wrapperl.env
   $ENV{PERL5LIB} = '/path/to/some/lib:/path/to/another/lib';
   $PERL = '/path/to/bin/perl';

   # You can have a different "wrapperl.env" on each directory.
   # What is the one that we would see from here? "wrapperl" can
   # tell you this with -e | --env
   shell$ wrapperl -e
   /path/to/wrapperl.env

   # If you also provide a parameter to -e | --env, it will tell
   # you which environment file will be seen by the provided
   # parameter (that is of course expected to be a path)
   shell$ wrapperl -e /path/to/a/wrapperl/symlink
   /path/to/a/wrapper.env

   # Time to make it work! Option -x | --exec means calling it
   # as if it were the chosen perl with the configurations in
   # "wrapperl.env"
   shell$ wrapperl -x -le 'print $^X'
   /path/to/bin/perl

   # This is actually the default behaviour if the first parameter
   # is not supported by "wrapperl" directly, so the following calls
   # are equivalent. So, use -x if you need to pass the called perl
   # options -d, -e, -s or -x, otherwise you can avoid it
   shell$ wrapperl -x myprogram.pl --foo bar # OR
   shell$ wrapperl myprogram.pl --foo bar

   # Option -s | --sibling allows to call programs that are
   # usually shipped with perl, e.g. perlthanks, podchecker, etc.
   shell$ wrapperl -s podchecker myprogram.pl

   # Another useful option is -d | --doc to call perldoc quickly,
   # so the following ones are equivalent:
   shell$ wrapperl -s perldoc Module::Name
   shell$ wrapperl -d Module::Name

   # You can symlink wrapperl and it will do some magic.
   # Call the real program a name ending with ".pl" (e.g. "prg.pl")
   # and symlink wrapperl with the same name withouth the extension
   # (e.g. "prg"). This is what will happen:
   shell$ ls -l
   -rw-r--r-- 1 me me 74 Apr 23 22:20 wrapperl.env
   lrwxrwxrwx 1 me me  8 Apr 23 22:51 prg -> /path/to/wrapperl
   -rwxr-xr-x 1 me me 74 Apr 23 22:28 prg.pl

   shell$ cat prg.pl
   #!/usr/bin/env perl
   print "using perl '$^X', \@INC contains:\n";
   print "- '$_'\n" for @INC;

   shell$ cat wrapperl.env
   $ENV{PERL5LIB} = '/path/to/some/lib:/path/to/another/lib';
   $PERL = '/path/to/bin/perl';

   shell$ which perl
   /usr/bin/perl

   # If you call the program directly, wrapperl is not used of course
   shell$ ./prg.pl
   using perl '/usr/bin/perl', @INC contains:
   - '/etc/perl'
   - '/usr/local/lib/perl/5.14.2'
   - '/usr/local/share/perl/5.14.2'
   - '/usr/lib/perl5'
   - '/usr/share/perl5'
   - '/usr/lib/perl/5.14'
   - '/usr/share/perl/5.14'
   - '/usr/local/lib/site_perl'
   - '.'

   # On the other hand, if you call the "prg" symlink to wrapperl,
   # the same program above will be called, but with the perl and
   # options set in "wrapperl.env"
   shell$ ./prg
   using perl '/path/to/bin/perl', @INC contains:
   - '/path/to/another/lib/i686-linux'
   - '/path/to/another/lib'
   - '/path/to/some/lib/i686-linux'
   - '/path/to/some/lib'
   - '/path/to/lib/site_perl/5.18.1/i686-linux'
   - '/path/to/lib/site_perl/5.18.1'
   - '/path/to/lib/5.18.1/i686-linux'
   - '/path/to/lib/5.18.1'
   - '.'

   # There are two symlinks/names that trigger a special behaviour,
   # namely "perl" and "perldoc" that do what you think
   shell$ ls -l
   lrwxrwxrwx 1 me me  8 Apr 23 21:51 perl -> /path/to/wrapperl
   lrwxrwxrwx 1 me me  8 Apr 23 22:46 perldoc -> /path/to/wrapperl

   # The following two are therefore equivalent (and no, the
   # double "-x" is not an error, because the first is consumed by
   # "wrapperl" and the second one is for the invoked perl)
   shell$ ./perl -x /path/to/my/program.pl
   shell$ wrapperl -x -x /path/to/my/program.pl

   # These three are equivalent too
   shell$ ./perldoc My::Module
   shell$ wrapperl -d My::Module
   shell$ wrapperl -s perldoc My::Module

=head1 DESCRIPTION

This program lets you wrap a perl program with some local-specific
configurations.

Why would you do this, e.g. as opposed to modifying the
I<hash-bang> line or setting C<PERL5LIB>, or calling the perl
executable directly? Well, lazyness of course, but also the fact
that in different environments the same program might need different
configurations, and changing those configurations possibly in many
little Perl programs quickly becomes an error-prone hassle.

C<wrapperl> provides you with a consistent, minimal and easy to setup
way to concentrate local-specific configurations in the
L<< The C<wrapperl.env> File >>, and be sure
that you will call your Perl program(s) with the right setup every time.

C<wrapperl>'s behaviour strongly depends on its name. That is, if
you leave it as C<wrapperl> it behaves in a specific way, while
if you name it differently then it does something else.

The easiest (and most robust) way to do the renaming is to use
symbolic links, if your filesystem allows you to. Otherwise, nothing
prevents you from copying C<wrapperl> to whatever name you need and
get the benefits described below.

The following sections start by describing the C<wrapperl.env> file
you should set up, then describe the behaviour in the different
conditions; among them, most probably you will be interested into
L</Named Something Else>.

=head2 The C<wrapperl.env> File

The C<wrapperl.env> file is at the heart of the localization of
your configurations.

=head3 Contents

The file is a standard Perl program. It will be called using whatever
I<default> perl is found, that is not what you are looking for most
probably (otherwise you would probably not be using C<wrapperl> at
all). You can do whatever setting inside it, while most probably you
will be interested in setting the environment variable C<PERL5LIB>
to point towards the library directories you want to include in
C<@INC>.

You can also affect how C<wrapperl> works by setting the following
variables in package C<main>:

=over

=item C<$ME>

the location of the original program invoked. When calling C<wrapperl>
with a different name (see L</Named Something Else>), it is used
together with C<$SUFFIX> described below to form the name of the
program C<$ME$SUFFIX> that will be called with the new C<$PERL>. In
general you should not need to fiddle with this.

=item C<$PERL>

the path to the perl to use for invoking the other programs.

By default it is set to the same perl that is executing C<wrapperl>,
namely C<$^X>, just in case you need to setup C<PERL5LIB>.

=item C<$PERLDOC>

The name of the C<perldoc> utility installed along with C<$PERL>.

By default it is set to C<perldoc>, and you probably do not need
to change it.

=item C<$SUFFIX>

a suffix that is appended to the name of the invoked program when
calling C<wrapperl> with a different name (see
L</Named Something Else>).

The called program will be C<$ME$SUFFIX>, so if C<$SUFFIX> is set
to C<.pl>, you are expected to call your I<real> program the same
as your symbolic link (or renamed C<wrapperl> program) but with
C<.pl> appended. Example:

   shell$ ls -l
   lrwxrwxrwx 1 me me  8 Apr 23 22:51 prg -> /path/to/wrapperl
   -rwxr-xr-x 1 me me 74 Apr 23 22:28 prg.pl

If your system is picky about how files should be named (e.g.
Windows might put some restrictions to what it considers as
I<executables>), then you can do your transformations directly
on C<$ME> and set C<$SUFFIX> to the empty string in order to
select the I<real> program to call.

By default, C<$SUFFIX> is set to C<.pl> and you should not need
to change it.

=back

=head3 Loading

The C<wrapperl.env> file is loaded via a C<do>, so you are warned
about any possible security issue.

The invocation is supposed to return a true value (in Perl terms),
otherwise the execution will be stopped.

=head3 Position

Depending on how C<wrapperl> is called, the C<wrapperl.env> file is
searched in different locations.

One or more
I<starting positions> will be considered, and used to perform a search
from that position upwards in the filesystem. For example, if the
starting point is C</path/to/some/internal/sub>, then the following
paths will be searched for C<wrapperl.env>:

   /path/to/some/internal/sub
   /path/to/some/internal
   /path/to/some
   /path/to
   /path
   /

An exception is thrown if no C<wrapperl.env> file is found during the
search in all the starting points.

The I<standard resolution> of the C<wrapperl.env> file is performed starting
from the current working directory, then from the user's home directory as
read from the C<HOME> environment variable.

In some cases, the starting position will be some other specific
location. For example, when C<wrapperl> is L</Named Something Else>,
the only starting location will be the path to the link to
C<wrapperl>, (i.e. what is used to initialize C<$ME>).

=head2 Direct Invocation

Direct invocation of C<wrapperl> (i.e. without changing the name
when calling it) is subject to the processing of some
options (see L</OPTIONS>).

Unless otherwise noted, the resolution of the C<wrapperl.env> file
is the I<standard> one as described in section L</Position>.

If none of the options in L</OPTIONS> is recognized, the selected
perl in C<$PERL> is invoked with whatever argument list is provided. This
is equivalent to using the C<-x|--exec> option, except of course that the
first option is not stripped away in this case.

Option C<-d|--doc> helps you call C<perldoc>, or whatever is set in
C<$PERLDOC>. This will be useful in order to use the C<perldoc> that is
shipped with the selected C<$PERL>, and more importantly with the same
options (e.g. C<PERL5LIB>) set in C<wrapperl.env>, so that you will be
able to find whatever module is installed in your personalized paths.

Option C<-e|--env> helps you find out what will be the C<wrapperl.env>
used, so that you can double check that it is the one you are expecting
and its contents. If you also pass a path in the command line, it will
be used as the starting point for searching C<wrapperl.env>, otherwise
the standard resolution process is used.

Option C<-s|--sibling> allows you to call one of the Perl programs
that are present in the same directory as C<$PERL>, much in the same
way as described for C<perldoc> above. For example, if you want to
check the POD documentation in C<YourModule.pm> using the C<podchecker>
that is shipped with the perl you indicated in C<wrapperl.env>:

   shell$ wrapperl -s podchecker

Last, option C<-x|--exec> allows you to call C<$PERL> with the options
set in C<wrapperl.env>. It is mostly unneeded, unless you want to
use any of the options above for calling C<$PERL>.

=head2 Named C<perl>

This name makes C<wrapperl> transform into a call to C<$PERL> as
possibly set by the C<wrapperl.env> file, including any command
line option provided. The behaviour is the same as calling
C<wrapperl> with option C<-x|--exec>.

The resolution of the C<wrapperl.env> file is performed according to
the I<standard resolution> process explained in section L</Position>.

=head2 Named <perldoc>

This name calls the C<perldoc> set in C<$PERLDOC> and located in the
same directory as C<$PERL>, including any command line option
provided. The behaviour is the same as calling C<wrapperl> with
option C<-d|--doc>.

The resolution of the C<wrapperl.env> file is performed according to
the I<standard resolution> process explained in section L</Position>.

=head2 Named Something Else

The normal usage is intended to ease wrapping a call to your Perl
program so that the right perl binary with the right libraries is
used, without sacrificing simplicity.

Assuming that you have set up your C<wrapperl.env> file (see
L<< /The C<wrapperl.env> File >>), you are only two steps away from
using C<wrapperl> to automate calling your program with the right
setup:

=over

=item *

you can write your program without worrying about which perl will
be used to call it or where the libraries are installed. Your only
constraint is to name it ending with C<$SUFFIX> or do some magic
to C<$ME> in C<wrapperl.env> (see L</Contents> for details).

By default, it suffices that you name your program ending with
C<.pl>. For example, we will assume that your program is called
C<prg.pl>.

=item *

You set up a copy to C<wrapperl> to be called the same as your
program, but without the C<$SUFFIX>. In the example, your copy
would be called C<prg>.

To make the copy you don't really have to make a copy! A symbolic
link is sufficient, if your filesystem supports them.

=back

This is really it! Now, every time you need to run your program...
don't do it, execute the C<wrapperl> copy instead! That is, in the
example you would call C<prg>, and it would in turn call your
C<prg.pl> but after reading all the configurations
in C<wrapperl.env>.

See L</TL;DR> for a complete and commented example.

=head1 OPTIONS

When invoked with name C<wrapperl>, this program supports the following
options. Note that you can provide one of them as the first option, and
anyone not appearing here will actually be used for invoking the
perl indicated in the C<wrapperl.env> file.

In all the options below, unless otherwise noted, the
I<standard resolution process> for searching C<wrapperl.env> is
used (see L</Position>).

=over

=item B<< -d | --doc [arg1 arg2 ...] >>

invoke whatever program is set in the C<$PERLDOC> variable in package
C<main> (C<perldoc> by default), using C<$PERL> and the settings inside
C<wrapperl.env>.

The C<$PERLDOC> program is expected to be placed in
the same directory as the selected C<$PERL>.

=item B<< -e | --env [path] >>

print the path to the C<wrapperl.env> file.

If a C<path> is provided after this option, it is used as a starting
location for searching C<wrapperl.env>, otherwise the
I<standard resolution process> is used. See L</Position> for additional
details.

=item B<< -s | --sibling name [arg1 arg2 ...] >>

invoke a I<sibling> program, i.e. a program that is shipped along
with C<$PERL> and is located in the same directory.

The program is run with C<$PERL> and the configurations set inside
C<wrapperl.env>. Any argument is provided on the command line is
passed along to the sibling program. This will thus work fine
when the sibling is a Perl program, but not for binary executables.

=item B<< -x | --exec [arg1 arg2 ...] >>

invoke C<$PERL> with the provided arguments, after loading the
options in C<wrapper.env>.

=back

=head1 DIAGNOSTICS

=over

=item C<< could not find wrapperl.env >>

C<wrapperl> tried to find C<wrapperl.env> in the same directory as
the symbolic link to it, or in any ancestor directory, but failed to
find one.

=item C<< errors loading '%s' >>

loading the C<wrapperl.env> was not successful, i.e. the invocation
via C<do> did not produce a true value. The placeholder provides
the location of the offending file.

=item C<< failed execution of %s >>

C<wrapperl> tried to execute the command (reported in the error message)
but failed. The placeholder provides the offending command.

=item C<< something went really wrong >>

you shouldn't ever see this message, if you do it's a bug!

=back

=head1 CONFIGURATION AND ENVIRONMENT

C<wrapperl> does not have a configuration per-se, but is of course
relying on the presence of a C<wrapperl.env> file for proper
functioning - see L</DESCRIPTION>.

=head1 DEPENDENCIES

C<wrapperl> relies on modules that are part of any standard Perl
distribution as of release 5.6.0.

=head1 BUGS AND LIMITATIONS

Please report bugs and hopefully solutions through the GitHub
repository at L<https://github.com/polettix/wrapperl>.

=head1 AUTHOR

Flavio Poletti <polettix@cpan.org>

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2015, Flavio Poletti C<polettix@cpan.org>.

This module is free software.  You can redistribute it and/or
modify it under the terms of the Artistic License 2.0. Please read
the full license in the F<LICENSE> file inside the distribution,
as you can find at L<https://github.com/polettix/wrapperl>.

This program is distributed in the hope that it will be useful,
but without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut
